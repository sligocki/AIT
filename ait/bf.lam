-- 830 bit brainfuck interpreter with
-- 8-bit cells and unbounded tape in both directions
-- input appears after a ']' uniquely delimiting the BF program
let
  id = \x.x;
  true = \x\y.x;
  false = \x\y.y;
  nil = false;
  not = \b\x\y.b y x;

  -- increment or decrement a bigendian fixed width number, ccps version
  step = \down.let step = \cont\done\list.list (\msb.step
             (\r\dummy.let nmsb=not msb in down msb nmsb cont done (\z.z nmsb r))
             (\r\dummy.                                       done (\z.z  msb r))
         ) (cont list) in step;
  op = \down. step down id id;

  -- this version is 4 bits shorter
  alu = \down.let alu = \list\cont.list
         (\msb\r\dummy.alu r (\ret\f.cont (\p.p (f msb) ret)
          (down (msb f id) (msb id f))))
         (cont list not) in alu;
  op = \down\cell. alu down cell true;
  -- iszero = \cell. alu true cell false false;

  -- foo post-applied to a list, returns body.loop body if all 0s, else id
  loop = \body.let loop = \cont\cell.
    cell (let foo = \b\l.b (l foo) (\c.body (loop c)) in foo) cont cell in loop;

  parse = \cont\bfs.bfs (\bf.let fls=false in bf fls fls fls (\b4\bf4.bf4 fls fls(\b1\bf1.bf1 (\b0\bla.
     let more=\sim1.parse (\sim2.cont (\cont.sim1 (sim2 cont))) in b4 (more (
      b0 (\cont\cell\right\left\input.b1 (input (\b.cont b right left))            -- ,
                                         (\z.z cell (cont cell right left input))) -- .
         (\cont\cell.cont (op b1 cell))))(                                         -- -/+
      b0 (more (\cont\cell\right\left.b1 (left  (\b.cont b  (\z.z cell right)))    -- <
                                         (right     cont    (\z.z cell left))))(   -- >
        b1 (cont id)                                                               -- ]
           (parse (\body.more (loop body))                                         -- [
    )))))));

  byte0 = let two=\f\x.f(f x); three=\f.f(f(f(\l\z.z true l))) in three two nil;
  rpt0 = let rpt=\z.z byte0 rpt in rpt
in parse (\sim.rpt0 (sim (\cell\right\left\input.nil)) sim)

-- test with
-- ./blc run8 bf.lam < hw.bf
-- which should output
-- Hello world!
-- or with
-- ./blc run8 bf.lam < thue-morse.bf
-- which outputs the thue morse sequence
